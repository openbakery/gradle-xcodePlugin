/*

Create a gradle.settings and add the snippet blow and fill in your values:

gradle.ext {
	hockeyappApiToken = "" // only needed when you want to upload to hockeykit
	hockeyappAppId = ""


	identity = 'iPhone Developer: ... '
	certificateURI = 'http://.../Development.p12'  // file:// urls also works here
	certificatePassword = 'password' // password to unlock to p12
	mobileProvisionURI = 'http://../Development.mobileprovision'

}

*/


buildscript {
	repositories {
		maven {
			url('http://openbakery.org/repository/')
			//url uri('/Users/rene/workspace/openbakery/xcodePlugin/repository')
		}
		mavenCentral()
	}
	dependencies {
		classpath group: 'org.openbakery', name: 'xcodePlugin', version: '0.9.13.b'
	}
}
apply plugin: 'xcode'

xcodebuild {
	scheme = 'Example'
	target = 'Example'
	configuration = 'Debug'

	// version = "6A1052d"


	signing {
		identity = gradle.identity                       // e.g. 'iPhone Developer: ...'
		certificateURI = gradle.certificateURI           // e.g 'http://example.com/Provisioning/Development.p12'
		certificatePassword = gradle.certificatePassword // e.g. 'secret'
		mobileProvisionURI = gradle.mobileProvisionURI   // e.g 'http://example.com/Provisioning/Development.mobileprovision'
	}
	
	hockeyapp {
	
		apiToken = gradle.hockeyappApiToken
		appID = gradle.hockeyappAppId
		
	}

}

/*
	Create custom tasks that 4 different kind of builds
*/

task continuous(dependsOn:'test') {
	description = 'Builds the app and runs all unit test in the Simulator for the iPhone and iPad'
}

task integration(dependsOn: 'hockeyapp') {
	description = 'Performs a build for the device and the resulting ipa is published unsign hockeykit'
}
task release(dependsOn: 'hockeykit') {
	description = 'Performs a build for the device that has the same configuration as the AppStore build, except is is distributed via hockeykit'
}

task appstore(dependsOn: 'archive') {
	description = 'Build for the AppStore'
}



gradle.taskGraph.whenReady { taskGraph ->

	if (taskGraph.hasTask(continuous) || taskGraph.hasTask(test)) {
		println "CONFIGURE INTEGRATION"
		xcodebuild {
			
			destination {
				platform = 'iOS Simulator'
				name = 'iPad Air'
				os = '8.0'
			}

			destination {
				platform = 'iOS Simulator'
				name = 'iPhone 4s'
				os = '8.0'
			}
			
		}
	}

	if (taskGraph.hasTask(integration)) {
		println "CONFIGURE INTEGRATION"
		xcodebuild {
			sdk = 'iphoneos'
		}
		infoplist {
			bundleIdentifier = 'org.openbakery.Example-Test'
		}
	}

	if (taskGraph.hasTask(release) || taskGraph.hasTask(appstore)) {
		println "CONFIGURE RELEASE"
		xcodebuild {
			configuration = 'Release'
			sdk = 'iphoneos'
		}
		infoplist {
			bundleIdentifier = 'org.openbakery.Example'
		}
	} 

	if (taskGraph.hasTask(appstore)) {
		println "CONFIGURE APPSTORE"
		xcodebuild {
			signing {
				identity = 'iPhone Distribution: ...'
				certificateURI = 'http://example.com/Provisioning/AppStore.p12'
				certificatePassword = 'secret'
				mobileProvisionURI = 'http://example.com/Provisioning/AppStore.mobileprovision'
			}
		}
	}
}
