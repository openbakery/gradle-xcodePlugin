buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:0.9.0"
  }
}



def versionNumber = '0.10.3'

if (project.hasProperty("buildNumber")) {
	versionNumber += "." + buildNumber 
}


group = 'org.openbakery'
version = versionNumber


apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: "com.gradle.plugin-publish"


sourceCompatibility = "1.6"
targetCompatibility = "1.6" 


configurations {
    deployerJars
}

repositories {
	mavenCentral()
}


dependencies {
	compile gradleApi()
	compile localGroovy()
	compile 'commons-io:commons-io:2.4+'
	compile 'commons-configuration:commons-configuration:1.10+'
	compile 'commons-collections:commons-collections:3.2.+'
	compile 'org.apache.httpcomponents:httpclient:4.3+'
	compile 'org.apache.httpcomponents:httpmime:4.3+'
	compile 'org.pegdown:pegdown:1.5.+'
	compile 'ch.qos.logback:logback-core:1.1.+'
	testCompile 'org.testng:testng:6.+'
	testCompile 'org.gmock:gmock:0.8.+'
	testCompile 'org.hamcrest:hamcrest-all:1.3'
	
  deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
	
}

sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDirs += ["src/main/java"]



uploadArchives {
	repositories {
		mavenDeployer {
      configuration = configurations.deployerJars
			
			repository(url: publishURL) {
				authentication(userName: publishUser, password: publishPassword)
			}
			
			pom.project {
				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
					}
				}
				developers {
			  	developer {
						id "renep"
			      name "Ren√© Pirringer"
			      email "rene@openbakery.org"
					}
				}
			}
		}
	}
}

test {
	useTestNG()
}


task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}



pluginBundle {
  website = 'http://openbakery.org/gradle.php'
  vcsUrl = 'https://github.com/openbakery/gradle-xcodePlugin'
  description = 'A gradle plugin for building Xcode projects!'
  tags = ['xcode', 'iOS', 'OS X']

  plugins {
    xcodeplugin {
      id = 'org.openbakery.xcodeplugin'
      displayName = 'Gradle Xcode plugin'
			version = versionNumber
    }
  }
}
