plugins {
	id "com.gradle.plugin-publish" version "0.12.0"
}


def publishURL = System.getenv("PUBLISH_URL")
def publishUser = System.getenv("PUBLISH_USER")
def publishPassword = System.getenv("PUBLISH_PASSWORD")

if (project.hasProperty("publishURL")) {
	publishURL = project.publishURL
}
if (project.hasProperty("publishUser")) {
	publishUser = project.publishUser
}
if (project.hasProperty("publishPassword")) {
	publishPassword = project.publishPassword
}



//cobertura.coverageFormats = ['html', 'xml']

apply plugin: 'maven'

repositories {
	mavenCentral()
}


configurations {
	deployerJars
}



dependencies {
	compile gradleApi()
	compile 'com.squareup.okhttp3:okhttp:4.2.0'
	compile 'org.openbakery.coverage:CoverageReport:0.9.4'
	deployerJars 'org.apache.maven.wagon:wagon-ssh:3.3.2'
}

jar {
	baseName 'xcode-plugin'
	from project.sourceSets.main.output
	from project(":libxcode").sourceSets.main.output
	from project(":libxcodetools").sourceSets.main.output
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	from project(":libxcode").sourceSets.main.allSource
	from project(":libxcodetools").sourceSets.main.allSource
	classifier = 'sources'
}

install {
	repositories.mavenInstaller {
		modifyPom(pom)
	}
}

uploadArchives {
	repositories {
		mavenDeployer {
			configuration = configurations.deployerJars

			repository(url: publishURL) {
				authentication(userName: publishUser, password: publishPassword)
			}

			modifyPom(pom)

			pom.project {
				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
					}
				}
				developers {
					developer {
						id "renep"
						name "RenÃ© Pirringer"
						email "rene@openbakery.org"
					}
				}
			}
		}
	}
}

def modifyPom(MavenPom pom) {
	pom.whenConfigured { p -> 
		p.dependencies = p.dependencies.findAll { dep -> 
			dep.artifactId != "libxcode" && dep.artifactId != "libxcodetools"
		}
	}
}

pluginBundle {
	website = 'https://openbakery.org/gxp/'
	vcsUrl = 'https://github.com/openbakery/gradle-xcodePlugin'
	description = 'A gradle plugin for building Xcode projects!'
	tags = ['xcode', 'iOS', 'os x', "osx", "mac", "iphone", "ipad"]

	plugins {
		xcodePlugin {
			id = 'org.openbakery.xcode-plugin'
			displayName = 'Gradle Xcode plugin'
		}
	}
}
