plugins {
	id "com.gradle.plugin-publish" version "0.9.1"
}



def versionNumber = '0.13.0'
def publishURL = System.getenv("PUBLISH_URL")
def publishUser = System.getenv("PUBLISH_USER")
def publishPassword = System.getenv("PUBLISH_PASSWORD")

if (project.hasProperty("publishURL")) {
	publishURL = project.publishURL
}
if (project.hasProperty("publishUser")) {
	publishUser = project.publishUser
}
if (project.hasProperty("publishPassword")) {
	publishPassword = project.publishPassword
}
if (project.hasProperty("buildNumber")) {
	versionNumber += "." + project.buildNumber 
}
if (project.hasProperty("versionSuffix")) {
	versionNumber += project.versionSuffix 
}


group = 'org.openbakery'
version = versionNumber


apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'

sourceCompatibility = "1.6"
targetCompatibility = "1.6" 

task wrapper(type: Wrapper) {
	gradleVersion = '2.8'
}

configurations {
	deployerJars
}

repositories {
	mavenCentral()
}


dependencies {
	compile gradleApi()
	compile localGroovy()
	compile 'commons-io:commons-io:1.4'
	compile 'commons-configuration:commons-configuration:1.10+'
	compile 'commons-collections:commons-collections:3.2.+'
	compile 'org.apache.httpcomponents:httpclient:4.3+'
	compile 'org.apache.httpcomponents:httpmime:4.3+'
	compile 'org.pegdown:pegdown:1.5.+'
	compile 'ch.qos.logback:logback-core:1.1.+'
	compile 'org.openbakery.coverage:CoverageReport:0.9.2'
	testCompile 'junit:junit:4.12'
	testCompile 'org.hamcrest:hamcrest-all:1.3'
	testCompile 'org.codehaus.groovy:groovy-all:2.4.4'
	testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
	testCompile 'cglib:cglib-nodep:3.1'
	deployerJars "org.apache.maven.wagon:wagon-ssh:2.10"
}


uploadArchives {
	repositories {
		mavenDeployer {
			configuration = configurations.deployerJars
			
			repository(url: publishURL) {
				authentication(userName: publishUser, password: publishPassword)
			}
			
			pom.project {
				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
					}
				}
				developers {
					developer {
						id "renep"
						name "Ren√© Pirringer"
						email "rene@openbakery.org"
					}
				}
			}
		}
	}
}


task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}


pluginBundle {
	website = 'http://openbakery.org/gradle.html'
	vcsUrl = 'https://github.com/openbakery/gradle-xcodePlugin'
	description = 'A gradle plugin for building Xcode projects!'
	tags = ['xcode', 'iOS', 'os x', "osx", "mac", "iphone", "ipad"]

	plugins {
		xcodePlugin {
			id = 'org.openbakery.xcode-plugin'
			displayName = 'Gradle Xcode plugin'
			version = versionNumber
		}
	}
}
