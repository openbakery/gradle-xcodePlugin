allprojects {
	apply plugin: 'java-gradle-plugin'
	apply plugin: 'groovy'

	def versionNumber = "0.15.4"

	if (project.hasProperty("versionNumber")) {
		versionNumber = project.versionNumber
	}
	if (project.hasProperty("versionSuffix")) {
		versionNumber += project.versionSuffix
	}
	if (project.hasProperty("buildNumber")) {
		versionNumber += "." + project.buildNumber
	}


	group = 'org.openbakery'
	version = versionNumber

	sourceCompatibility = "1.6"
	targetCompatibility = "1.6"

	repositories {
		mavenCentral()
	}

	dependencies {
		compile localGroovy()
		compile 'commons-io:commons-io:2.2'
		compile 'commons-configuration:commons-configuration:1.10'
		compile 'commons-collections:commons-collections:3.2.2'
		compile 'commons-codec:commons-codec:1.10'
		compile 'ch.qos.logback:logback-core:1.0.13'

		testCompile 'junit:junit:4.12'
		testCompile 'org.hamcrest:hamcrest-all:1.3'
		testCompile 'org.codehaus.groovy:groovy-all:2.4.12'
		testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
		testCompile 'org.objenesis:objenesis:1.2'
		testCompile 'cglib:cglib-nodep:2.2.2'
		testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
			exclude module: 'groovy-all'
		}
	}

	// Fix warnings when running tests in IDEA. Example:
	// SLF4J: Class path contains multiple SLF4J bindings.
	//    SLF4J: Found binding in [jar:file:/Users/ben/.gradle/caches/4.6/generated-gradle-jars/gradle-api-4.6.jar!/org/slf4j/impl/StaticLoggerBinder.class]
	// SLF4J: Found binding in [jar:file:/Users/ben/.gradle/caches/modules-2/files-2.1/ch.qos.logback/logback-classic/1.1.5/92353eb144695bba80b31f7bec4f36d871f230ac/logback-classic-1.1.5.jar!/org/slf4j/impl/StaticLoggerBinder.class]
	// SLF4J: See http://www.slf4j.org/codes.html#multiple_bindings for an explanation.
	// SLF4J: Actual binding is of type [org.gradle.internal.logging.slf4j.OutputEventListenerBackedLoggerContext]
	configurations.all {
		resolutionStrategy.dependencySubstitution {
			substitute module('ch.qos.logback:logback-classic') with module('org.slf4j:slf4j-api:1.7.+')
		}
	}
}



project(':plugin') {
	dependencies {
		compileOnly project(':libxcode')
		testCompile project(':libtest')
	}
}


project(':libxcode') {
	dependencies {
		testCompile project(':libtest')
	}
}

project(':libtest') {
	dependencies {
		compile project(':libxcode')
	}
}
